plugins {
	id 'java'
	id 'jacoco'
	id 'checkstyle'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.sonarqube' version '5.1.0.4882'
}

group = 'hexlet.code'
version = '0.0.1-SNAPSHOT'
description = 'Project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

jacoco {
	toolVersion = "0.8.11"
}

checkstyle {
	toolVersion = '10.12.4'
	configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
}

sonarqube {
	properties {
		property "sonar.projectKey", "irinakomarchenko_java-project-99"
		property "sonar.organization", "irinakomarchenko03"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
	}
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

	implementation 'io.sentry:sentry-spring-boot-starter:7.13.0'
	implementation 'io.sentry:sentry-logback:7.13.0'

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	testImplementation("net.datafaker:datafaker:2.1.0")
	testImplementation("org.instancio:instancio-core:3.3.0")
	testImplementation("com.jayway.jsonpath:json-path:2.9.0")

	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
	systemProperty "spring.profiles.active", "test"
}


jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

tasks.withType(JavaCompile) {
	options.annotationProcessorPath = configurations.annotationProcessor
}